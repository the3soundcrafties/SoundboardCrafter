apply plugin: 'com.android.application'

android {
    //noinspection SpellCheckingInspection
    signingConfigs {
        config
    }
    compileSdk 34
    defaultConfig {
        applicationId "de.soundboardcrafter"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        versionName "0.2.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
            minifyEnabled false
        }
        staging {
            initWith debug
            debuggable = true
            signingConfig signingConfigs.config
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'de.soundboardcrafter'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.core:core:1.12.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.media:media:1.7.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation "com.google.android.material:material:1.11.0"
    implementation 'com.google.guava:guava:28.0-jre'
    implementation 'org.jetbrains:annotations:17.0.0'
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
}

// See https://medium.com/@chauyan/how-to-use-properties-file-to-do-android-sign-action-in-gradle-45d51e31a98c
// and https://gist.github.com/SleeplessByte/89c90f702c0f2fdc2b12

File localPropsFile = file('../local.properties')
if (localPropsFile.exists()) {
    // On local android
    Properties localProps = new Properties()
    localProps.load(new FileInputStream(localPropsFile))
    assert localProps['keystore.props.file']

    Properties keyProps = new Properties()
    keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))

    android.signingConfigs.config.storeFile = file(keyProps["RELEASE_STORE_FILE"])
    android.signingConfigs.config.storePassword = keyProps["RELEASE_STORE_PASSWORD"]
    android.signingConfigs.config.keyAlias = keyProps["RELEASE_KEY_ALIAS"]
    android.signingConfigs.config.keyPassword = keyProps["RELEASE_KEY_PASSWORD"]
} else {
    // Within the Github workflow, e.g.
    android.buildTypes.release.signingConfig = null
    android.buildTypes.debug.signingConfig = null
    android.buildTypes.staging.signingConfig = null
}